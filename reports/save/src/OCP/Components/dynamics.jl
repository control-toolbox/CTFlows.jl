"""
$(TYPEDSIGNATURES)

Set the full dynamics of the optimal control problem `ocp` using the function `f`.

# Arguments
- `ocp::PreModel`: The optimal control problem being defined.
- `f::Function`: A function that defines the complete system dynamics.

# Preconditions
- The state and times must be set before calling this function.
- Control is **optional**: problems without control input (dimension 0) are supported.
- No dynamics must have been set previously.

# Behavior
This function assigns `f` as the complete dynamics of the system. It throws an error
if the state or times are not yet set, or if dynamics have already been set.

# Errors
Throws `Exceptions.PreconditionError` if called out of order or in an invalid state.
"""
function dynamics!(ocp::PreModel, f::Function)::Nothing
    @ensure __is_state_set(ocp) Exceptions.PreconditionError(
        "State must be set before defining dynamics",
        reason="state has not been defined yet",
        suggestion="Call state!(ocp, dimension) before dynamics!",
        context="dynamics! function - state validation",
    )
    @ensure __is_times_set(ocp) Exceptions.PreconditionError(
        "Times must be set before defining dynamics",
        reason="time horizon has not been defined yet",
        suggestion="Call times!(ocp, t0, tf) or times!(ocp, N) before dynamics!",
        context="dynamics! function - times validation",
    )
    @ensure !__is_dynamics_set(ocp) Exceptions.PreconditionError(
        "Dynamics already set",
        reason="dynamics have already been defined for this OCP",
        suggestion="Create a new OCP instance or use partial_dynamics! for additional dynamics",
        context="dynamics! function - duplicate definition check",
    )

    # set the dynamics
    ocp.dynamics = f

    return nothing
end

"""
$(TYPEDSIGNATURES)

Add a partial dynamics function `f` to the optimal control problem `ocp`, applying to the
subset of state indices specified by the range `rg`.

# Arguments
- `ocp::PreModel`: The optimal control problem being defined.
- `rg::AbstractRange{<:Int}`: Range of state indices to which `f` applies.
- `f::Function`: A function describing the dynamics over the specified state indices.

# Preconditions
- The state and times must be set before calling this function.
- Control is **optional**: problems without control input (dimension 0) are supported.
- The full dynamics must not yet be complete.
- No overlap is allowed between `rg` and existing dynamics index ranges.

# Behavior
This function appends the tuple `(rg, f)` to the list of partial dynamics. It ensures
that the specified indices are not already covered and that the system is in a valid
configuration for adding partial dynamics.

# Errors
Throws `Exceptions.PreconditionError` if:
- The state or times are not yet set.
- The dynamics are already defined completely.
- Any index in `rg` overlaps with an existing dynamics range.

# Example
```julia-repl
julia> dynamics!(ocp, 1:2, (out, t, x, u, v) -> out .= x[1:2] .+ u[1:2])
julia> dynamics!(ocp, 3:3, (out, t, x, u, v) -> out .= x[3] * v[1])
```
"""
function dynamics!(ocp::PreModel, rg::AbstractRange{<:Int}, f::Function)::Nothing
    @ensure __is_state_set(ocp) Exceptions.PreconditionError(
        "State must be set before defining partial dynamics",
        reason="state has not been defined yet",
        suggestion="Call state!(ocp, dimension) before partial dynamics!",
        context="partial_dynamics! function - state validation",
    )
    @ensure __is_times_set(ocp) Exceptions.PreconditionError(
        "Times must be set before defining partial dynamics",
        reason="time horizon has not been defined yet",
        suggestion="Call times!(ocp, t0, tf) or times!(ocp, N) before partial dynamics!",
        context="partial_dynamics! function - times validation",
    )
    @ensure !__is_dynamics_complete(ocp) Exceptions.PreconditionError(
        "Complete dynamics already set",
        reason="dynamics have already been completely defined for this OCP",
        suggestion="Use partial_dynamics! before setting complete dynamics, or create a new OCP instance",
        context="partial_dynamics! function - complete dynamics check",
    )

    # Check indices in rg are within valid state index bounds
    for i in rg
        if i < 1 || i > state_dimension(ocp)
            throw(
                Exceptions.IncorrectArgument(
                    "Dynamics index out of bounds";
                    got="index=$i",
                    expected="index in range [1, $(state_dimension(ocp))]",
                    suggestion="Use indices in 1:$(state_dimension(ocp)), e.g., dynamics!(ocp, 1:2, f)",
                    context="dynamics! index validation",
                ),
            )
        end
    end

    # initialize dynamics container if needed
    if isnothing(ocp.dynamics)
        ocp.dynamics = Vector{Tuple{UnitRange{Int},Function}}()
    elseif ocp.dynamics isa Function
        throw(
            Exceptions.PreconditionError(
                "Cannot add partial dynamics to complete dynamics";
                reason="dynamics already defined as a single function",
                suggestion="Use partial_dynamics! calls instead of dynamics! function, or create a new OCP instance",
                context="partial_dynamics! function - dynamics type conflict",
            ),
        )
    end

    # check that indices in rg are not already covered
    for (existing_range, _) in ocp.dynamics
        for i in rg
            if i in existing_range
                throw(
                    Exceptions.PreconditionError(
                        "Dynamics range overlap";
                        reason="index $i in range already has assigned dynamics",
                        suggestion="Use a non-overlapping range or remove existing dynamics first",
                        context="partial_dynamics! function - range overlap check",
                    ),
                )
            end
        end
    end

    # push the new partial dynamics
    push!(ocp.dynamics, (rg, f))

    return nothing
end

"""
$(TYPEDSIGNATURES)

Define partial dynamics for a single state variable index in an optimal control problem.

This is a convenience method for defining dynamics affecting only one element of the state vector. It wraps the scalar index `i` into a range `i:i` and delegates to the general partial dynamics method.

# Arguments
- `ocp::PreModel`: The optimal control problem being defined.
- `i::Integer`: The index of the state variable to which the function `f` applies.
- `f::Function`: A function of the form `(out, t, x, u, v) -> ...`, which updates the scalar output `out[1]` in-place.

# Behavior
This is equivalent to calling:
```julia-repl
julia> dynamics!(ocp, i:i, f)
```

# Errors
Throws the same errors as the range-based method if:
- The model is not properly initialized.
- The index `i` overlaps with existing dynamics.
- A full dynamics function is already defined.

# Example
```julia-repl
julia> dynamics!(ocp, 3, (out, t, x, u, v) -> out[1] = x[3]^2 + u[1])
```
"""
function dynamics!(ocp::PreModel, i::Integer, f::Function)::Nothing
    return dynamics!(ocp, i:i, f)
end

"""
$(TYPEDSIGNATURES)

Build a combined dynamics function from multiple parts.

This function constructs an in-place dynamics function `dyn!` by composing several sub-functions, each responsible for updating a specific segment of the output vector.

# Arguments
- `parts::Vector{<:Tuple{<:AbstractRange{<:Int}, <:Function}}`: 
  A vector of tuples, where each tuple contains:
  - A range specifying the indices in the output vector `val` that the corresponding function updates.
  - A function `f` with the signature `(output_segment, t, x, u, v)`, which updates the slice of `val` indicated by the range.

# Returns
- `dyn!`: A function with signature `(val, t, x, u, v)` that updates the full output vector `val` in-place by applying each part function to its assigned segment.

# Details
- The returned `dyn!` function calls each part function with a view of `val` restricted to the assigned range. This avoids unnecessary copying and allows efficient updates of sub-vectors.
- Each part function is expected to modify its output segment in-place.

# Example
```julia-repl
# Define two sub-dynamics functions
julia> f1(out, t, x, u, v) = out .= x[1:2] .+ u[1:2]
julia> f2(out, t, x, u, v) = out .= x[3] * v

# Combine them into one dynamics function affecting different parts of the output vector
julia> parts = [(1:2, f1), (3:3, f2)]
julia> dyn! = __build_dynamics_from_parts(parts)

val = zeros(3)
julia> dyn!(val, 0.0, [1.0, 2.0, 3.0], [0.5, 0.5], 2.0)
julia> println(val)  # prints [1.5, 2.5, 6.0]
```
"""
function __build_dynamics_from_parts(
    parts::Vector{<:Tuple{<:AbstractRange{<:Int},<:Function}}
)::Function
    function dyn!(val, t, x, u, v)
        for (rg, f!) in parts
            f!(@view(val[rg]), t, x, u, v)
        end
        return nothing
    end
    return dyn!
end
